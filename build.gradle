/*
 *    Copyright 2018 Duncan Sterken
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

group = "me.duncte123"
archivesBaseName = "weebJava"
version = "2.1.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile group: 'com.github.natanbc', name: 'reliqua', version: '2.3.2'
    compile group: 'com.afollestad', name: 'ason', version: '1.4.16'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.1'
}

javadoc {
    failOnError = false
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addStringOption('-html5')
    exclude('me/duncte123/weebJava/models/impl')
    exclude('me/duncte123/weebJava/models/reputation/impl')
    exclude('me/duncte123/weebJava/models/settings/impl')
    exclude('me/duncte123/weebJava/web')
    exclude('me/duncte123/weebJava/exceptions')
    exclude('me/duncte123/weebJava/helpers')
}

buildConfig {
    appName = project.name
    version = project.version
    packageName = project.group + "." + project.archivesBaseName
    charset = 'UTF-8'

    clsName = 'WeebInfo'
}
task sourcesForRelease(type: Copy) {
    from('src/main/java') {
        include '**/WeebInfo.java'
        filter(ReplaceTokens, tokens: [
                version: version
        ])
    }
    into 'build/filteredSrc'
}

task generateJavaSources(type: SourceTask) {
    def javaSources = sourceSets.main.allJava.filter {
        it.name != 'WeebInfo.java'
    }
    source = javaSources + sourcesForRelease.destinationDir

    dependsOn sourcesForRelease
}

compileJava {
    options.encoding = 'UTF-8'
    source = generateJavaSources.source

    dependsOn generateJavaSources
}

jar {
    classifier = null
}

task sourcesJar(type: Jar, dependsOn: classes) {
    /*from sourceSets.main.output
    from sourceSets.main.allSource*/
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        MavenProject(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version = version

            artifact javadocJar
            artifact sourcesJar
        }
    }
}

publishing {
    repositories {
        maven {
            url "https://dl.bintray.com/duncte123/weeb.java/maven-metadata.xml"
        }
    }
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn javadocJar
    dependsOn sourcesJar

    jar.mustRunAfter clean
    javadocJar.mustRunAfter jar
    sourcesJar.mustRunAfter javadocJar
}

task renamePom(type: Copy) {
    from('build/publications/MavenProject/pom-default.xml')
    into('build/publications/MavenProject/')
    rename('pom-default.xml', "${archivesBaseName}-${version}.pom")
}

task buildRelease(dependsOn: build) {
    dependsOn publishToMavenLocal
    dependsOn renamePom

    publishToMavenLocal.mustRunAfter build
    renamePom.mustRunAfter publishToMavenLocal

}